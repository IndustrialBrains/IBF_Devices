<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="Fb_DevCylWithFbk" Id="{4d944a84-dab5-48bc-b450-758ff0ff83b4}" SpecialFunc="None">
    <Declaration><![CDATA[{attribute 'reflection'}
FUNCTION_BLOCK PUBLIC Fb_DevCylWithFbk EXTENDS FB_DevValve
VAR_INPUT
	bIcfFbkOpenPresent 	: BOOL := FALSE ; // Feedback for cylinder open is present 
	bIcfFbkClosedPresent: BOOL := FALSE ; // Feedback for Cylinder closed is present
	bIcfCheckTooSlow	: BOOL := TRUE 	; // Check if cylinder is moving too slow
	bIcfCheckTooFast	: BOOL := TRUE 	; // Check if cylinder is moving too fast
	nMargin				: LREAL:= 10	; // % of tollerance in move speed 
END_VAR
VAR_OUTPUT
	// ===== Teach function =======
	bCmdCopyTimeParam 	: BOOL	:= FALSE 	;// Cylinder, Command to higher level copy toucht time to parameters
	bFeClosed			: BOOL 	:= FALSE	; // closed sensor falling edge
	bReClosed			: BOOL 	:= FALSE	; // closed sensor rising edge
	bFeOpen				: BOOL 	:= FALSE 	; // Open sensor falling edge
	bReOpen				: BOOL 	:= FALSE 	; // open sensor rising edge 
	bReOpenSlow         : BOOL  := FALSE    ; // rising edge, opening went too slow
	bReOpenFast         : BOOL  := FALSE    ; // rising edge, opening went too fast
	bReCloseSlow        : BOOL  := FALSE    ; // rising edge, closing went too slow
	bReCloseFast        : BOOL  := FALSE    ; // rising edge, closing went too fast
END_VAR
VAR	
	bFcoCalcDone	: BOOL := FALSE ; // Calculate timers done 
	
	stTimesClose	: Udt_TimerBandwith ; // Timer bandwith for cylinder close
	stTimesOpen		: Udt_TimerBandwith	; // Timer bandwitch for cylinder open 
	
	fbSensorClosed	: Fb_DevDigitalInputFunctions ; // Digital sensor function closed detection
	fbSensorOpen	: Fb_DevDigitalInputFunctions ; // Digital sensor function open detection
	
	fbTimerOpen		: TON ; // Timer measuring cylinder open output has been active
	fbTimerClose 	: TON ; // Timer measuring cylinder close output has been active
	fbTimerTeach	: TON ; // Timer for teaching cylinder 

	rtCmdWriteParam	: R_TRIG ; // Write parameter rising edge trigger 
	// Simulation
	fbTimerSimOpen 	: TON 	; 
	fbTimerSimClose	: TON 	; 
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[// set device type
	IF eDevType = E_Devices.Undefined THEN 
		eDevType := E_Devices.Cylinder ;
	END_IF

// call base function 
	SUPER^();

// check for device init
	IF NOT bInitOk THEN
		RETURN;
	END_IF 

// ====== Check direction of movement =======
	ExecDirection();
	
// ====== Reset movement indicators =======
	bReOpenSlow  := FALSE;
	bReOpenFast  := FALSE;
	bReCloseSlow := FALSE;
	bReCloseFast := FALSE;

// ====== Check movement speed =======
	ExecTimeout();
	
// ====== Calculate timers =======
	CalcTimers();

// Reduce fault level when in manual mode
IF bManual THEN
	stFault.FaultType		:= E_FaultTypes.OW;
ELSE
	stFault.FaultType		:= E_FaultTypes.CF;
END_IF
]]></ST>
    </Implementation>
    <Folder Name="Private" Id="{a3827919-0a79-428f-9a70-8c1e3fcbad7b}" />
    <Method Name="CalcTimers" Id="{d777a46f-a2e6-426c-b6f6-e27ce1214e62}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE CalcTimers : BOOL
VAR
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[IF stParTimeToClose.fValue = stTimesClose.Nominal AND stParTimeToOpen.fValue = stTimesOpen.Nominal AND bFcoCalcDone THEN 
	RETURN ;
ELSE
	bFcoCalcDone := FALSE ;
END_IF
 
// ====== Calculate close times ======
	stTimesClose.Nominal 	:= stParTimeToClose.fValue ;
	stTimesClose.Maximum 	:= stParTimeToClose.fValue * (1 + (nMargin / 100));
	stTimesClose.Minimal 	:= stParTimeToClose.fValue * (1 - (nMargin / 100));

// ====== Calculate open times ======
	stTimesOpen.Nominal 	:= stParTimeToOpen.fValue ;
	stTimesOpen.Maximum 	:= stParTimeToOpen.fValue * (1 + (nMargin / 100));
	stTimesOpen.Minimal 	:= stParTimeToOpen.fValue * (1 - (nMargin / 100));
	
// ====== Set feedback =======
	bFcoCalcDone := TRUE ;

// ===== send write parameters command ======	
	rtCmdWriteParam(CLK := bFcoCalcDone);
	bCmdCopyTimeParam := rtCmdWriteParam.Q	;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="CmdSimulate" Id="{6e22c863-00b6-4acc-b064-897ffbaba65f}">
      <Declaration><![CDATA[METHOD PUBLIC CmdSimulate : BOOL
VAR_INPUT
	bFbkReverse		: BOOL := FALSE ; 
	tTimeSimOpen	: TIME := T#1S 	;
	tTimeSimClose	: TIME := T#1S 	;  
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Run timers when cylinder is active 
	fbTimerSimOpen	(IN := q_Open	, PT := tTimeSimOpen);
	fbTimerSimClose	(IN := q_Close	, PT := tTimeSimClose);

// set feedback sensors to simulate
	fbSensorOpen.bCmdSimulate 	:= TRUE ;
	fbSensorClosed.bCmdSimulate := TRUE ;

// set simulated feedback state 
	IF bFbkReverse THEN 
		fbSensorClosed.bSimState:= fbTimerSimOpen.Q 	;
		fbSensorOpen.bSimState	:= fbTimerSimClose.Q 	; 	
	ELSE 
		fbSensorClosed.bSimState:= fbTimerSimClose.Q 	;
		fbSensorOpen.bSimState	:= fbTimerSimOpen.Q 	; 	
	END_IF	
]]></ST>
      </Implementation>
    </Method>
    <Method Name="CmdTeach" Id="{91bc0cb5-62eb-4ee0-9533-355dbae1c792}">
      <Declaration><![CDATA[METHOD PUBLIC CmdTeach : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// ====== turn off time check function until new calculation cycle =======
	bFcoCalcDone := false ;

// ====== teach cylinder from hardware inputs ======
	fbTimerTeach	(PT:= T#60S);
	fbTimerSwitchDir(PT:= T#1S); 
// ====== time cylinder cycles ======
	// ===== Start test loop =====
	IF bFco_Closed AND fbTimerSwitchDir.Q THEN
		bCmdClose 			:= FALSE;
		bCmdOpen 			:= TRUE ;
		fbTimerSwitchDir.IN	:= FALSE; 
		fbTimerTeach.IN 	:= TRUE ;
	ELSIF bFco_Open AND fbTimerSwitchDir.Q THEN
		bCmdClose 			:= TRUE ;
		bCmdOpen 			:= FALSE;
		fbTimerSwitchDir.IN	:= FALSE; 
		fbTimerTeach.IN 	:= TRUE ;
	ELSIF NOT bCmdClose AND NOT bCmdOpen THEN 
		bCmdClose 			:= TRUE ;
		bCmdOpen 			:= FALSE;
		fbTimerTeach.IN 	:= TRUE ;
	END_IF

// ====== start timing ========
	IF bCmdClose AND bFco_Closed THEN 
		IF fbTimerTeach.IN THEN
			stParTimeToClose.fValue 	:= TIME_TO_LREAL(fbTimerTeach.ET);
		END_IF
		fbTimerSwitchDir.IN := TRUE 		;
		fbTimerTeach.IN 	:= FALSE 		;
	ELSIF bCmdOpen AND bFco_Open THEN 
		IF fbTimerTeach.IN THEN
			stParTimeToOpen.fValue		:= TIME_TO_LREAL(fbTimerTeach.ET);
		END_IF
		fbTimerSwitchDir.IN := TRUE 		;
		fbTimerTeach.IN 	:= FALSE 		;
	END_IF
]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExecDirection" Id="{7b6b02f6-752b-46a5-bdce-1b0af61f0130}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE ExecDirection : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// ===== Check direction commands =====
 	IF q_Close AND fbSensorOpen.bReStatus AND NOT q_Open THEN
		stFault.Active			:= TRUE ;
		stFault.Description		:= 'Cylinder opening instead of closing';
		bErrorActive			:= TRUE 								;
	ELSIF q_Open AND fbSensorClosed.bReStatus AND NOT q_close THEN
		stFault.Active			:= TRUE ;
		stFault.Description		:= 'Cylinder closing instead of opening';
		bErrorActive			:= TRUE 								;
	END_IF

]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExecSetFeedback" Id="{1ffe37d7-2aa6-4e39-bf13-5b9a981a51d6}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PROTECTED ExecSetFeedback : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[{warning disable C0371}
// ====== Call sensor close function =======
	IF bIcfFbkClosedPresent THEN 
		fbSensorClosed();
		bFco_Closed := fbSensorClosed.bFcoInputActive ;
		bFeClosed 	:= fbSensorClosed.bFeStatus ;
		bReClosed 	:= fbSensorClosed.bReStatus ;
	ELSE 
		fbTOnClose	(IN 	:= q_Close, PT 	:= LREAL_TO_TIME(stParTimeToClose.fValue))	;
		bFco_Closed := fbTOnClose.Q ;
	END_IF

// ====== Call sensor open function =======
	IF bIcfFbkOpenPresent THEN 
		fbSensorOpen() 	;
		bFco_open 	:= fbSensorOpen.bFcoInputActive ;
		bFeOpen		:= fbSensorOpen.bFeStatus;
		bReOpen		:= fbSensorOpen.bReStatus;
	ELSE 
		FbTOnOpen	(IN 	:= q_Open, PT 	:= LREAL_TO_TIME(stParTimeToOpen.fValue))	;
		bFco_Open := FbTOnOpen.Q ;
	END_IF

{warning restore C0371}
// ====== set outputs =======
	bReady		:= bFco_Closed OR bFco_Open ;]]></ST>
      </Implementation>
    </Method>
    <Method Name="ExecTimeout" Id="{4c13fd3f-2cd5-4918-a271-53ad554a49b6}" FolderPath="Private\">
      <Declaration><![CDATA[METHOD PRIVATE ExecTimeout
VAR
	bEnableCheck : BOOL;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[bEnableCheck := (bManual OR bEnable OR (bInitOk AND NOT bIsSafe)) AND bFcoCalcDone;

//====== Run timeout checks for cylinder close =======
IF bEnableCheck AND bIcfFbkClosedPresent THEN 
	IF fbSensorClosed.bReStatus THEN 
		IF bIcfCheckTooFast AND (TIME_TO_LREAL(fbTimerClose.ET) < stTimesClose.Minimal) THEN 
			// send message to HMI 
			stFault.Active			:= TRUE 							;
			stFault.Description		:= 'Cylinder closing too fast';
			bErrorActive			:= TRUE 							;
			bReCloseFast			:= TRUE                             ;
		END_IF
	ELSIF bIcfCheckTooSlow AND fbTimerClose.Q THEN
		// send message to HMI 
		stFault.Active			:= TRUE ;
		stFault.Description		:= 'Cylinder closing too slow';
		bErrorActive			:= TRUE 								;
		bReCloseSlow			:= TRUE                             	;
	END_IF
END_IF	

//====== Run timeout checks for cylinder Open =======
IF bEnableCheck AND bIcfFbkOpenPresent THEN 
	IF fbSensorOpen.bReStatus THEN
		IF bIcfCheckTooFast AND (TIME_TO_REAL(fbTimerOpen.ET) < stTimesOpen.Minimal) THEN 
			// send message to HMI 
			stFault.Active			:= TRUE ;
			stFault.Description		:= 'Cylinder opening too fast';
			bErrorActive			:= TRUE 							;
			bReOpenFast 			:= TRUE 							;
		END_IF
	ELSIF bIcfCheckTooSlow AND fbTimerOpen.Q THEN
		// send message to HMI 
		stFault.Active			:= TRUE ;
		stFault.Description		:= 'Cylinder opening too slow';
		bErrorActive			:= TRUE 							;
		bReOpenSlow 			:= TRUE 							;
	END_IF
END_IF	

//======= run timers =======
fbTimerClose(
	IN := bEnableCheck 
		AND q_Close
		AND	(NOT fbSensorClosed.bFcoInputActive),
	PT := LREAL_TO_TIME(stTimesClose.Maximum)
);
fbTimerOpen(
	IN := bEnableCheck
		AND q_Open
		AND (NOT fbSensorOpen.bFcoInputActive),
	PT := LREAL_TO_TIME(stTimesOpen.Maximum)
);
]]></ST>
      </Implementation>
    </Method>
    <Method Name="Init" Id="{db3610d9-73cc-44d4-8df3-a5e2149be4ab}">
      <Declaration><![CDATA[METHOD PUBLIC Init : BOOL
VAR_INPUT
	// Message location data
	nId			: UINT ; // Identification number of object
	nParentId	: UDINT ; // Id of higher level If aplicable (0 = no parent)
	// Command state
	eResetState		: E_ValveResetState := 0		; // Cylinder reset state variable (default is idle) 
	eSafeState		: E_ValveResetState	:= 0		; // Valve safe state
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[bFcoCalcDone := FALSE;

// Enable sensor feedback
fbSensorClosed.bEmbedded := TRUE ;
fbSensorClosed.bEnable	:= TRUE ;
fbSensorClosed.Init	(	nId 		:= 0,
						nParentId 	:= stIdentity.nLocationId) ;

fbSensorOpen.bEmbedded := TRUE ;
fbSensorOpen.bEnable	:= TRUE ;
fbSensorOpen.Init	(	nId 		:= 1,
						nParentId 	:= stIdentity.nLocationId) ;

// Call feedback sensors 
ExecSetFeedback(); 

SUPER^.Init(nId, nParentId, eResetState, eSafeState);

// ===== Set device type for manual control =====
eDevType := E_Devices.Cylinder ;

bInitOk := fbSensorClosed.bInitOk 	AND bInitOk ;
bInitOk := fbSensorOpen.bInitOk		AND bInitOk ;
Init 	:= bInitOk ;]]></ST>
      </Implementation>
    </Method>
  </POU>
</TcPlcObject>